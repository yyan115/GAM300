plugins {
    alias(libs.plugins.android.application)
}

android {
    namespace 'com.gam300.game'
    compileSdk 36

    defaultConfig {
        applicationId "com.gam300.game"
        minSdk 34
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        ndk {
            abiFilters 'arm64-v8a'
        }
        
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

// Task to build the Engine library before Android native build
task buildEngine(type: Exec) {
    description 'Build the GAM300 Engine library for Android'
    group 'build'

    // Set working directory to the main project
    workingDir = file('../../Project')

    // Build the engine using cmake
    commandLine 'cmake', '--build', 'Build/android-debug', '--config', 'Debug'

    // Only run if build directory exists (engine has been configured)
    onlyIf {
        file('../../Project/Build/android-debug').exists()
    }
}

// Make all native build tasks depend on engine build
tasks.whenTaskAdded { task ->
    if (task.name.contains('external') && task.name.contains('Native') && task.name.contains('Build')) {
        task.dependsOn buildEngine
    }
}

// Also make sure the engine is built before any assembleDebug/assembleRelease
tasks.matching { it.name.startsWith('assemble') }.all {
    dependsOn buildEngine
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    implementation libs.constraintlayout
    implementation files('libs/fmod.jar')
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}